plugins {
	id "org.xtext.xtend" version "1.0.21" // 24-01-2018
}

apply plugin: 'application'
apply plugin: 'java'
apply plugin: 'eclipse'

sourceCompatibility = 1.8

eclipse {
	project {
	  name = 'yamtl.examples.mapping.incr.cd2db'
	  buildCommand 'org.eclipse.jdt.core.javabuilder'
	  buildCommand 'org.eclipse.buildship.core.gradleprojectbuilder'
	  buildCommand 'org.eclipse.wst.common.project.facet.core.builder'

	  natures 'org.eclipse.jdt.core.javanature',
		  'org.eclipse.buildship.core.gradleprojectnature',
		  'org.eclipse.wst.common.project.facet.core.nature',
		  'org.eclipse.xtext.ui.shared.xtextNature',
		  'org.eclipse.pde.PluginNature'
	}
}

repositories {
	maven {
		url "https://github.com/yamtl/yamtl.github.io/raw/mvn-repo/mvn-repo/snapshot-repo"
	}
	mavenLocal()
	mavenCentral()
    jcenter()
}


ext {
    gradleVer = '4.6'
    xtendVersion = '2.13.0'
}

task wrapper(type: Wrapper) {
	gradleVersion = gradleVer
}



// XTEND: configures output directories for generated java
xtend {
	sourceSets {
		main.xtendOutputDir = 'src/main/xtend-gen'
		test.xtendOutputDir = 'src/test/xtend-gen'
	}
}

sourceSets {
	main {
		java {
			srcDirs = [
				'src/main/java',
				'src/main/emf-gen',
				'src/main/xtend-gen'
			]
		}
	}
}



// 	INCREMENTAL SETUP
mainClassName = "cd2db.Runner_incr_delta1"
//mainClassName = "cd2db.Runner_incr_delta2"
//mainClassName = "cd2db.Runner_incr_delta3"
//mainClassName = "cd2db.Runner_incr_delta4"
//mainClassName = "cd2db.Runner_incr_delta5"
//mainClassName = "cd2db.Runner_incr_delta6"
applicationDefaultJvmArgs = ["-Xmx12288m"]


// ASPECTJ
configurations {
	ajc
	aspects
	aspectCompile
	ajInpath
	compile {
		extendsFrom aspects
	}
}

compileJava {
	sourceCompatibility="1.8"
	targetCompatibility="1.8"
  
	doLast{
		ant.taskdef( resource:"org/aspectj/tools/ant/taskdefs/aspectjTaskdefs.properties", classpath: configurations.ajc.asPath )
		ant.iajc(
			source:"1.8",
			target:"1.8",
			destDir: sourceSets.main.output.classesDir.absolutePath,
			inpath: configurations.ajInpath.asPath,
			maxmem: "512m",
			fork: "true",
			aspectPath:configurations.aspects.asPath,
			sourceRootCopyFilter:"**/.git/*,**/*.java",
			classpath:"${configurations.compile.asPath};${configurations.aspectCompile.asPath}"){
			sourceroots{
				sourceSets.main.java.srcDirs.each{
					pathelement(location:it.absolutePath)
				}
			}
		}
	}
  }

dependencies {
	compile "org.eclipse.xtend:org.eclipse.xtend.lib:$xtendVersion"
	compile "yamtl:yamtl:0.0.2-SNAPSHOT"
	compile 'org.eclipse.emf:org.eclipse.emf.ecore.change:2.11+'
	
	// AspectJ
	ajc "org.aspectj:aspectjtools:1.9.1"
	compile("org.aspectj:aspectjrt:1.9.1")
	compile("org.aspectj:aspectjweaver:1.9.1")
	aspects "yamtl:yamtl:0.0.2-SNAPSHOT"
	ajInpath "yamtl:yamtl:0.0.2-SNAPSHOT"
}

